/*
Copyright 2015-2018 Hermann Krumrey<hermann@krumreyh.com>

This file is part of android-dice.

android-dice is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

android-dice is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with android-dice.  If not, see <http://www.gnu.org/licenses/>.
*/

ext {
    version = "4.1.3"
    versionCode = 040103
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:+"
    }
}

// Shared operations for sub-projects
subprojects {

    // Define Repositories for submodules
    repositories {
        jcenter()
    }
    apply plugin: 'com.android.application'
    apply plugin: 'kotlin-android'

    configurations {
        ktlint
    }

    // Define common submodule dependencies
    dependencies {
        compile 'com.android.support:appcompat-v7:+'
        compile 'com.android.support:support-v4:+'
        compile 'com.android.support:cardview-v7:+'
        ktlint 'com.github.shyiko:ktlint:+'
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:+"
        compile "org.jetbrains.anko:anko-commons:+"
    }

    // Define ktlint task
    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }
    check.dependsOn ktlint
}

// Print version
task version(type: VersionTask)
class VersionTask extends DefaultTask {
    @TaskAction
    def version() {
        println project.ext.version
    }
}
